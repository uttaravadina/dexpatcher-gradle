/*
 * DexPatcher - Copyright 2015-2020 Rodrigo Balerdi
 * (GNU General Public License version 3 or later)
 *
 * DexPatcher is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published
 * by the Free Software Foundation, either version 3 of the License,
 * or (at your option) any later version.
 */

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
}

group = 'com.github.lanchon.dexpatcher'
version = '2.1.1'

ext.mainArtifact = 'dexpatcher-gradle'
ext.artifactName = 'DexPatcher-gradle'

repositories {
    jcenter()
    google()
}

dependencies {
    implementation localGroovy()
    compileOnly 'com.android.tools.build:gradle:3.6.0'
}

compileGroovy {
    options.compilerArgs << '-proc:none'
}

apply from: 'configure-artifacts.gradle'

afterEvaluate {
    def archivesArtifacts = configurations.archives.artifacts
    archivesArtifacts.removeAll archivesArtifacts.findAll { it.classifier in ['javadoc', 'groovydoc'] }
}

pluginBundle {

    website = 'https://dexpatcher.github.io/'
    vcsUrl = 'https://github.com/DexPatcher/DexPatcher-gradle'

    description = 'DexPatcher is a toolchain for modifying Android APK files at source-level using Java, ' +
            'graphical resource editors, and the full power of Android Studio in all major platforms.'

    tags = ['android', 'application', 'apk', 'dexpatcher', 'apktool', 'dex2jar', 'patch', 'modify', 'reverse']

    mavenCoordinates {
        groupId = group
        artifactId = mainArtifact
    }

}

gradlePlugin {

    plugins {

        basePlugin {
            id = 'com.github.lanchon.dexpatcher.base'
            implementationClass = 'lanchon.dexpatcher.gradle.plugins.DexpatcherBasePlugin'
            displayName = 'DexPatcher Base'
            description = 'The DexPatcher Base plugin provides basic DexPatcher, Apktool and dex2jar tasks. ' +
                    'DexPatcher is free software. (GPLv3+)'
        }

        apkLibraryPlugin {
            id = 'com.github.lanchon.dexpatcher.apk-library'
            implementationClass = 'lanchon.dexpatcher.gradle.plugins.ApkLibraryPlugin'
            displayName = 'DexPatcher APK Library'
            description = 'The DexPatcher APK Library plugin processes a source application provided as an Android APK ' +
                    'and produces a DexPatcher APK library. The library is used to create modified versions of the ' +
                    'source application and includes its Dalvik bytecode and decoded manifest and resources. ' +
                    'DexPatcher is free software. (GPLv3+)'
        }

        patchedAppPlugin {
            id = 'com.github.lanchon.dexpatcher.patched-application'
            implementationClass = 'lanchon.dexpatcher.gradle.plugins.PatchedAppPlugin'
            displayName = 'DexPatcher Patched Application'
            description = 'The DexPatcher Patched Application plugin produces a modified Android application by applying ' +
                    'changes to code, manifest and resources of a source application provided as an Android APK or ' +
                    'a DexPatcher APK library. DexPatcher is free software. (GPLv3+)'
        }

        patchLibraryPlugin {
            id = 'com.github.lanchon.dexpatcher.patch-library'
            implementationClass = 'lanchon.dexpatcher.gradle.plugins.PatchLibraryPlugin'
            displayName = 'DexPatcher Patch Library'
            description = 'The DexPatcher Patch Library plugin produces a DexPatcher patch library that bundles compiled ' +
                    'changes to code, manifest and resources of a source application without bundling any part of the '  +
                    'application itself. The source application is provided as an Android APK or a DexPatcher APK library. ' +
                    'DexPatcher patch libraries are similar in structure to Android libraries (AAR files). ' +
                    'The changes bundled in a patch library are applied to its source application using the DexPatcher ' +
                    'Patched Application plugin. Several patch libraries can be applied in the same step or their ' +
                    'application can be serialized and chained. DexPatcher is free software. (GPLv3+)'
        }

    }

}

apply from: 'check-copyright.gradle'

wrapper.distributionType = Wrapper.DistributionType.ALL
